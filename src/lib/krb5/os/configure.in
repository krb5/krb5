AC_INIT(configure.in)
CONFIG_RULES
dnl time checks are for timeofday.c (which gets them from osconf.h)
dnl and gmt_mktime.c (which only gets them from here...)
AC_TIME_WITH_SYS_TIME
AC_HAVE_HEADERS(unistd.h sys/time.h regex.h regexp.h regexpr.h)

dnl regcomp is present but non-functional on Solaris 2.4
AC_C_CROSS
AC_MSG_CHECKING(for working regcomp)
AC_CACHE_VAL(ac_cv_func_regcomp,[
AC_TRY_RUN([
#include <sys/types.h>
#include <regex.h>
regex_t x; regmatch_t m;
int main() { return regcomp(&x,"pat.*",0) || regexec(&x,"pattern",1,&m,0); }
], ac_cv_func_regcomp=yes, ac_cv_func_regcomp=no)])
AC_MSG_RESULT($ac_cv_func_regcomp)
test $ac_cv_func_regcomp = yes && AC_DEFINE(HAVE_REGCOMP)
save_LIBS="$LIBS"
LIBS=-lgen
dnl this will fail if there's no compile/step in -lgen, or if there's
dnl no -lgen.  This is fine.
AC_CHECK_FUNCS(compile step)
LIBS="$save_LIBS"
AC_HAVE_FUNCS(re_comp)
AC_HEADER_EGREP(time_t, sys/types.h, AC_DEFINE(POSIX_TYPES))
HAS_ANSI_VOLATILE
AC_DEFINE(AN_TO_LN_RULES)
USE_ANAME
V5_SHARED_LIB_OBJS
V5_USE_SHARED_LIB
KRB5_LIBRARIES
KRB5_RUN_FLAGS
SubdirLibraryRule([${OBJS}])
V5_AC_OUTPUT_MAKEFILE
