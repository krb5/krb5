AC_INIT(telnetd.c)
CONFIG_RULES
AC_PROG_INSTALL
AC_CHECK_LIB(util,main)
USE_ANAME
AC_CHECK_LIB(termcap,main,AC_DEFINE(TERMCAP)
LIBS="$LIBS -ltermcap",
  AC_CHECK_LIB(curses,setupterm,LIBS="$LIBS -lcurses")
)
AC_HEADER_CHECK(termios.h,AC_DEFINE(USE_TERMIO) ac_termio=1)
if test -z "$ac_termio"; then
AC_HEADER_CHECK(termio.h,AC_DEFINE(SYSV_TERMIO),ac_sysv_termio=1)
if test -z "$ac_sysv_termio"; then
  AC_MSG_CHECKING([for cc_t in termio.h])
  AC_CACHE_VAL(krb_cv_type_cc_t,
  [AC_TRY_LINK([cc_t],[#include <termio.h>],
  [cc_t foo;],krb_cv_type_cc_t=yes,krb_cv_type_cc_t=no)])
  AC_MSG_RESULT($krb_cv_type_cc_t)
  if test $krb_cv_type_cc_t = no; then
    AC_DEFINE(NO_CC_T)
  fi
fi
fi
AC_HEADER_TIME
AC_CHECK_HEADERS(string.h arpa/nameser.h)
AC_CHECK_HEADERS(sys/time.h sys/tty.h)
AC_HAVE_FUNCS(vhangup)
dnl Make our operating system-specific security checks and definitions for
dnl login.
dnl
case $krb5_cv_host in
*-*-hpux*)
     broken_streams=yes
     ;;
*-*-irix*)
     # Irix doesn't have a working granpt, and more over
     # you can't push anything onto a pty, so telnetd really
     # Really wants to treat it as if it doesn't have streams
     broken_streams=yes
     ;;
esac
if test -z "$broken_streams" ; then
    AC_FUNC_CHECK(grantpt,AC_DEFINE(STREAMSPTY))
fi
AC_HEADER_CHECK(sac.h,AC_DEFINE(HAS_SAC))
AC_HEADER_CHECK(sys/ptyvar.h,AC_DEFINE(HAS_PTYVAR))
AC_FUNC_CHECK(gettosbyname,AC_DEFINE(HAS_GETTOS))
AC_HEADER_CHECK(sys/filio.h,AC_DEFINE(FILIO_H))
AC_HEADER_CHECK(sys/stream.h,AC_DEFINE(STREAMS))
AC_HEADER_CHECK(utmpx.h,
	AC_DEFINE(UTMPX)
	AC_DEFINE(HAVE_UTMPX_H)
	ac_utmp_h=1)
if test -z "$ac_utmp_h" ; then
AC_CHECK_HEADERS(utmp.h)
fi
AC_FUNC_CHECK(cgetent,AC_DEFINE(HAS_CGETENT))
AC_MSG_CHECKING([if setpgrp takes two arguments])
dnl
AC_CACHE_VAL(krb5_cv_sys_setpgrp_two,
[AC_TRY_LINK( 
[#include <unistd.h>],[setpgrp(0,0)],
krb5_cv_sys_setpgrp_two=yes,krb5_cv_sys_setpgrp_two=no)])
AC_MSG_RESULT($krb5_cv_sys_setpgrp_two)
if test $krb5_cv_sys_setpgrp_two = yes; then
  AC_DEFINE(SETPGRP_TWOARG)
fi
dnl
dnl For the encryption code we need libdes425
dnl Fix post beta-6
withval=no
AC_ARG_WITH([krb4],[],,withval=yes)
if test $withval = no; then
	KRB4_CRYPTO_LIB='-ldes425'
	DEPKRB4_CRYPTO_LIB='$(TOPLIBD)/libdes425.a'
fi
dnl
USE_KRB4_LIBRARY
KRB5_LIBRARIES
V5_USE_SHARED_LIB
V5_AC_OUTPUT_MAKEFILE
